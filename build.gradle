import java.nio.file.Files
import java.nio.file.Paths

plugins {
    id 'java'
    id 'application'
    id "io.freefair.lombok" version "6.0.0-m2"
}

group 'de.richardliebscher'
version '0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.swagger.parser.v3:swagger-parser:2.0.25'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.0'
    implementation 'info.picocli:picocli:4.6.1'
    implementation 'commons-io:commons-io:2.8.0'
    testImplementation 'com.eclipsesource.minimal-json:minimal-json:0.9.5'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

application {
    mainClass = 'de.richardliebscher.openapi_json_schema_generator.Main'
}

task createProperties(dependsOn: processResources) {
    doLast {
        Files.createDirectories(Paths.get("$buildDir/resources/main/de/richardliebscher/openapi_json_schema_generator"));
        new File("$buildDir/resources/main/de/richardliebscher/openapi_json_schema_generator/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['name'] = project.name.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}