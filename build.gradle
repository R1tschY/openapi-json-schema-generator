import java.nio.file.Files
import java.nio.file.Paths

plugins {
    id 'java'
    id 'application'
    id "io.freefair.lombok" version "6.0.0-m2"
    id 'net.researchgate.release' version '3.0.1'
}

group 'de.richardliebscher.openapi'
version project.property("version")

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.swagger.parser.v3:swagger-parser:2.1.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
    implementation 'info.picocli:picocli:4.6.3'
    implementation 'commons-io:commons-io:2.11.0'
    testImplementation 'com.eclipsesource.minimal-json:minimal-json:0.9.5'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

test {
    useJUnitPlatform()
}

application {
    mainClass = 'de.richardliebscher.openapi_json_schema_generator.Main'
}

release {
    tagTemplate = 'v${version}'

    git {
        requireBranch.set('master')
    }
}

task createProperties(dependsOn: processResources) {
    doLast {
        Files.createDirectories(Paths.get("$buildDir/resources/main/de/richardliebscher/openapi_json_schema_generator"))
        new File("$buildDir/resources/main/de/richardliebscher/openapi_json_schema_generator/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['name'] = project.name.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}